$!==============================================================================
$!
$!  Program:  go-vms.com
$!  Created:  06/19/97
$!  Author:   David Sowder (davids@cosmic.swau.edu)
$!
$!  Program Summary: A DCL wrapper for RC5 on VMS
$!
$!  Modifications:
$!    xx/xx/xx      Who            Description of Change
$!    12/14/97      David Sowder   Added menu for compile and link options
$!
$!==============================================================================
$!
$ IF P1 .EQS "RECHOOSE" THEN GOTO BUILD
$ START:
$ RC564_1 = F$SEARCH( "RC564.EXE;" )
$ IF RC564_1 .EQS. "" THEN GOTO BUILD
$ WRITE SYS$OUTPUT "You can now use the 'RC564' command from the $ prompt"
$ RC564 == "$''RC564_1'"
$ IF P1 .EQS "RECHOOSE" THEN GOTO DONE
$ SET PROCESS/PRIORITY=0
$ DEFINE/USER SYS$INPUT TT:
$ RC564 'P1
$ SET PROCESS/PRIORITY=4
$ GOTO DONE
$!
$ QUIT:
$ WRITE SYS$OUTPUT "RC5 startup terminated abnormally."
$ DONE:
$ EXIT
$!
$ BUILD:
$ NET :== ""
$ NETNAM :== ""
$ PROCOP :== ""
$ LNK :== ""
$!
$ WRITE SYS$OUTPUT "RC564.EXE not found.  Needing to link one."
$ DEFINE/NOLOG/USER SYS$INPUT TT:
$ WRITE SYS$OUTPUT "1 - Multinet"
$ WRITE SYS$OUTPUT "2 - UCX"
$ WRITE SYS$OUTPUT "3 - No Network"
$ WRITE SYS$OUTPUT ""
$ READ/PROMPT="Enter desired network option: " SYS$COMMAND CMD
$ WRITE SYS$OUTPUT ""
$ WRITE SYS$OUTPUT ""
$ IF CMD .EQ. 1 THEN NET :== "MULTINET"
$ IF CMD .EQ. 1 THEN NETNAM :== "MULTINET"
$ IF CMD .EQ. 2 THEN NET :== "__VMS_UCX__"
$ IF CMD .EQ. 2 THEN NETNAM :== "UCX"
$ IF CMD .EQ. 3 THEN NET :== "NONETWORK"
$ IF CMD .EQ. 3 THEN NETNAM :== "NONET"
$ IF NET .EQS. "" THEN GOTO QUIT
$!
$ DEFINE/NOLOG/USER SYS$INPUT TT:
$ WRITE SYS$OUTPUT "1 - EV4"
$ WRITE SYS$OUTPUT "2 - EV5"
$ WRITE SYS$OUTPUT ""
$ READ/PROMPT="Enter desired processor option: " SYS$COMMAND CMD
$ WRITE SYS$OUTPUT ""
$ WRITE SYS$OUTPUT ""
$ IF CMD .EQ. 1 THEN PROCOP :== "EV4"
$ IF CMD .EQ. 2 THEN PROCOP :== "EV5"
$ IF PROCOP .EQS. "" THEN GOTO QUIT
$!
$ DEFINE/NOLOG/USER SYS$INPUT TT:
$ WRITE SYS$OUTPUT "1 - Use Executable"
$ WRITE SYS$OUTPUT "2 - Link locally to solve shared library problems"
$ WRITE SYS$OUTPUT ""
$ READ/PROMPT="Enter desired link option: " SYS$COMMAND CMD
$ WRITE SYS$OUTPUT ""
$ WRITE SYS$OUTPUT ""
$ IF CMD .EQ. 1 THEN LNK :== "EXEC"
$ IF CMD .EQ. 2 THEN LNK :== "LIB"
$ IF LNK .EQS. "" THEN GOTO QUIT
$!
$ IF LNK .EQS. "LIB" THEN GOTO MAKE_LIBRARY
$ COPY RC564-'PROCOP'-'NETNAM'.EXE RC564.EXE
$ PURGE RC564.EXE
$ GOTO START
$!
$ MAKE_LIBRARY:
$ IF NET .EQS. "MULTINET" THEN GOTO MAKE_LIBRARY_MULTINET
$ LIBRARY/EXTRACT=RC5/OUTPUT=RC5.OBJ RC564-'PROCOP'-'NETNAM'
$ LINK/EXECUTABLE=RC564 RC5,RC564-'PROCOP'-'NETNAM'/LIBRARY
$ PURGE RC564.EXE
$ GOTO START
$!
$ MAKE_LIBRARY_MULTINET:
$ LIBRARY/EXTRACT=RC5/OUTPUT=RC5.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=CLICONFIG/OUTPUT=CLICONFIG.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=CLIENT/OUTPUT=CLIENT.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=AUTOBUFF/OUTPUT=AUTOBUFF.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=INIREAD/OUTPUT=INIREAD.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=NETWORK/OUTPUT=NETWORK.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=SCRAM/OUTPUT=SCRAM.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=BUFFWORK/OUTPUT=BUFFWORK.OBJ RC564-'PROCOP'-'NETNAM'
$ LIBRARY/EXTRACT=MAIL/OUTPUT=MAIL.OBJ RC564-'PROCOP'-'NETNAM'
$ LINK/EXECUTABLE=RC564-'PROCOP'-'NETNAM' -
  	RC5,CLICONFIG,CLIENT,AUTOBUFF,INIREAD,NETWORK,SCRAM,BUFFWORK,MAIL, -
	MULTINET.OPT/OPT
$ COPY RC564-'PROCOP'-'NETNAM'.EXE RC564.EXE
$ PURGE RC564.EXE
$ GOTO START

